class nest::base::firewall {
  class { 'firewalld':
    default_zone => 'drop',
  }

  # Don't manage the service state
  Service <| title == 'firewalld' |> {
    ensure => undef,
  }

  # Configure the zones that this module uses
  # See https://www.linuxjournal.com/content/understanding-firewalld-multi-zone-configurations
  firewalld_zone {
    'external':
      interfaces => [$facts['networking']['primary']],
      target     => 'DROP',
    ;

    'internal':
      interfaces => ['tun0'],
      target     => 'ACCEPT',
    ;
  }

  # Purge direct rules
  firewalld_direct_purge { 'rule': }

  # Purge unmanaged zones
  tidy { '/etc/firewalld/zones':
    matches => [
      'block.xml*',
      'dmz.xml*',
      'drop.xml*',
      'home.xml*',
      'public.xml*',
      'trusted.xml*',
      'work.xml*',
    ],
    recurse => 1,
    notify  => Class['firewalld::reload'],
  }
}
