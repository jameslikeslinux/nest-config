# Install or upgrade a service
#
# Wrapper around KubeCM for Nest compatibility
#
# @param service    Installation name (what is this deployment called)
# @param app        Chart name, or your name for this deployment and set `chart_source`
# @param chart      Typically `repo_name/chart_name` or an `oci://` URI, but could be a local path,
#                     a valid Puppet file source, or `undef` for no chart (just Hiera resources).
# @param deploy     Run or skip the deployment
# @param hooks      Enable or disable install hooks
# @param namespace  Kubernetes namespace to manage
# @param render_to  Just save the fully-rendered chart to this yaml file
# @param repo_url   Optional URL of the Helm repo to add
# @param restore    Masks backup job during restore deployment
# @param version    Optional Helm chart version
# @param wait       Wait for resources to become available
# @param subcharts  Additional charts to deploy as part of this one
plan nest::kubernetes::deploy (
  String           $service,
  String           $app       = $service,
  Optional[String] $chart     = undef,
  Boolean          $deploy    = true,
  Boolean          $hooks     = true,
  Optional[String] $namespace = undef,
  Optional[String] $render_to = undef,
  Optional[String] $repo_url  = undef,
  Boolean          $restore   = false,
  Optional[String] $version   = undef,
  Boolean          $wait      = false,
  Array[Hash]      $subcharts = [],
) {
  # Handle empty string args (thanks YAML plans)
  if $render_to and $render_to != '' {
    $render_to_real = $render_to
  } else {
    $render_to_real = undef
  }

  # Don't trash backup during restore
  if $restore {
    $remove_resources = ['backup']
  } else {
    $remove_resources = []
  }

  # Give extra time for any VIPs to propagate
  if $wait and !$render_to_real {
    $sleep = 10
  } else {
    $sleep = undef
  }

  # Prepare subchart definitions for KubeCM
  $subcharts_modified = $subcharts.map |$subchart| {
    $subchart + {
      'release'      => $subchart['service'],
      'chart'        => $subchart['app'],
      'chart_source' => $subchart['chart'],
    } - ['service', 'app']
  }

  return run_plan('kubecm::deploy', {
    release          => $service,
    chart            => $app,
    chart_source     => $chart,
    deploy           => $deploy,
    hooks            => $hooks,
    namespace        => $namespace,
    remove_resources => $remove_resources,
    render_to        => $render_to_real,
    repo_url         => $repo_url,
    version          => $version,
    wait             => $wait,
    sleep            => $sleep,
    subcharts        => $subcharts_modified,
  })
}
